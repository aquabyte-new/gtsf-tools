PROJ := 'hh-gtsf-monitor'

# ECR repo and tags
ECR_REPO := "286712564201.dkr.ecr.eu-west-1.amazonaws.com/research"
LATEST_TAG := PROJ + '-latest'
DATE_TAG := PROJ + '-' + shell('date -I')


# Display commands
@help:
    just --list


# Set up repo
@init:
    echo "3.8" > .python-version
    uv sync


# Format code
@fmt:
    uv run ruff check --select I --fix *.py
    uv run ruff format *.py


# Build image
@build:
    echo Building docker image...
    tar -czhf ui.tar.gz --dereference ui-dist
    docker build -q -t {{PROJ}} .
    

# Run the docker container locally
@run:
    docker run \
        -ti \
        --rm \
        --network host \
        {{PROJ}} /bin/bash


# Release the image to ECR
@release: build
    # for tag in {{LATEST_TAG}} {{DATE_TAG}}; do \
    #     echo Pushing ${tag}...; \
    #     docker tag {{PROJ}} "{{ECR_REPO}}:${tag}"; \
    #     docker push -q "{{ECR_REPO}}:${tag}"; \
    # done
    echo Pushing {{LATEST_TAG}}...
    docker tag {{PROJ}} "{{ECR_REPO}}:{{LATEST_TAG}}"
    docker push -q "{{ECR_REPO}}:{{LATEST_TAG}}"
